/*
 * This file was initially generated by the Gradle 'init' task.
 */

plugins {
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.akka.io/maven')
    }
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

dependencies {
    // Used for testing
    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    // Used by the application.
    implementation platform(libs.akka.bom)
    implementation libs.akka.actor.typed
}

test {
    useJUnitPlatform()
    // in this project, test classes start with the word 'Test'
    include '**/Test*.class'
    testLogging {
        events 'started', 'passed', 'failed', 'skipped'
    }
}

group = 'edu.akka.sample'
version = '1.0-SNAPSHOT'
description = 'Actor Model Sample App on Java using Akka'
java.sourceCompatibility = '21'

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

jar {
    archiveBaseName = "sample-akka-$project.name-all"
    manifest {
        attributes 'Implementation-Title' : 'Sample Akka App',
                   'Implementation-Version' : version,
                   'Main-Class' : 'edu.akka.sample.app.classic.TransactionsProcessor'
    }
    from {
        //duplicatesStrategy = DuplicatesStrategy.INHERIT
        project.configurations.runtimeClasspath.collect {
            File file -> {

                if (file.getName().contains("actor-typed")) {
                    project.zipTree(file).matching {
                        // file "reference.conf" is duplicated, i.e.,
                        // it exists in both akka typed and untyped
                        // actor JAR's
                        // however, in the typed one, this file does
                        // not contain the "akka.loggers" configuration
                        // because of that, it will be excluded
                        exclude "reference.conf"
                    }
                }
                else {
                    project.zipTree(file)
                }
            }
        }
    }
}
